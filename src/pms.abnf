; (Unofficial) Gentoo Package Management Standard (PMS) ABNF
; Copyright (C) 2013  Walter Stanish
;
;  This program is free software: you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation, either version 3 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 

; === Naming Scheme ===
;  * Normal entities are named as follows.
;     * Top level entities conclude with the suffix '-name'
;     * Components of those entities generally include a further suffix,
;       for instance '-name-first-char'
;     * Specific, terminal entities are named uniquely.
;  * Ontological entities are named differently.
;     * Top level ontological categories are named using the suffix '-types'
;       (to avoid collision with the 'category' entity within PMS).
;     * Other ontological category names include the string '-type-'.


; === EAPI (2.1, 2.2, 2.3, 3.1.7) ===
; structural view
eapi-name                   = eapi-name-first-char 0*eapi-name-char
eapi-name-first-char        = capital-letter / lowercase-letter / digit / underscore
eapi-name-char              = eapi-name-first-char / hyphen / dot / plus

; ontological view
eapi-name-types             = eapi-name-type-numeric / eapi-name-type-special
eapi-name-type-numeric      = eapi-name-type-issued / eapi-name-type-reserved
eapi-name-type-issued       = eapi-0 / eapi-1 / eapi-2 / eapi-3 / eapi-4 / eapi-5
eapi-name-type-reserved     = eapi-6 / eapi-7 / eapi-8 / eapi-9
eapi-name-type-special      = eapi-name-type-paludis-eapi
eapi-name-type-paludis-eapi = "paludis-" 0*eapi-name-char

; specific entities
eapi-0                      = "0"
eapi-1                      = "1"
eapi-2                      = "2"
eapi-3                      = "3"
eapi-4                      = "4"
eapi-5                      = "5"
eapi-6                      = "6"
eapi-7                      = "7"
eapi-8                      = "8"
eapi-9                      = "9"


; === Names (3.1) ===
; ontological view
name-types                  = category-name / package-name / slot-name


; === Category Names (3.1.1) ===
; structural view
category-name               = category-name-first-char 0*category-name-char
category-name-first-char    = capital-letter / lowercase-letter / digit / underscore / dot
category-name-char          = category-name-first-char / hyphen / dot / plus

; === Package Names (3.1.2) ===
;  This is a hack due to the look-behind assertion used in the PMS document,
;  ie. "... must not end in a hyphen followed by one or more digits". 
;  (Is this avoidable?)
;
; structural view
package-name                = package-name-inner 0*hyphen
package-name-inner          = package-name-nohyphen / package-name-hyphen
package-name-first-char     = capital-letter / lowercase-letter / digit / underscore
package-name-char           = package-name-first-char / plus
;  - cannot be first character
;package-name-nondigitplus  = capital-letter / lowercase-letter / underscore
package-name-nondigit       = capital-letter / lowercase-letter / underscore / plus
;  - covers 'f0o', 'fo0', 'f0o', 'f', '_', '00mf_____', '_0', '_+'.
;    (and with the package-name entity, variants with trailing -'s)
package-name-nohyphen       = package-name-first-char 0*package-name-char
;  - covers 'fo0-goo', 'fo0-m+o0-g00', 'f0o-6o0', 'f0o-60o' denies 'fo0-000'.
;    (and with the package-name entity, variants with trailing -'s)
package-name-nohyphen       = package-name-nohyphen-part / 1*package-name-nohyphen-parts
package-name-nohyphen-part  = package-name-first-char 0*package-name-char hyphen package-name-end
package-name-nohyphen-parts = package-name-nohyphen-part hyphen
;  - special case for string following the final hyphen (must contain one nondigit char)
package-name-end            = 0*package-name-char package-name-nondigit 0*package-name-char


; === Qualified Package Names (3.1.2) ===
; structural view
qualified-package-name            = category-name forward-slash package-name


; === General Reference Entities ===
capital-letter              = "A" / "B" / "C" / "D" / "E" / "F" / "G" / "H" / "I" / "J" / "K" / "L" / "M" / "N" / "O" / "P" / "Q" / "R" / "S" / "T" / "U" / "V" / "W" / "X" / "Y" / "Z"
lowercase-letter            = "a" / "b" / "c" / "d" / "e" / "f" / "g" / "h" / "i" / "j" / "k" / "l" / "m" / "n" / "o" / "p" / "q" / "r" / "s" / "t" / "u" / "v" / "w" / "x" / "y" / "z"
digit                       = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"
underscore                  = "_"
hyphen                      = "-"
dot                         = "."
plus                        = "+"
forward-slash               = "/"
